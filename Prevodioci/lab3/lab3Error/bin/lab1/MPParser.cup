package lab1;

import java_cup.runtime.*;
import java.io.*;

parser code {:
   public int errNo = 0;
   
   public static void main( String[] args )
   {
      try
      {
         FileReader file = new FileReader( args[0] );
         MPLexer scanner = new MPLexer( file );
         MPParser parser = new MPParser( scanner );
         parser.parse();
         if ( parser.errNo == 0 )
            System.out.println( "Analiza zavrsena. U kodu nema gresaka." );
         else
            System.out.println( "Analiza zavrsena. Broj gresaka: " + parser.errNo );
      }
      catch( Exception e )
      {
         e.printStackTrace();
      }
   }
   
   public void syntax_error(Symbol cur_token) { }
   
   public void report_error(String message, Object info)
   {
        System.out.print( message );
   }

   public int getLine() {
      return ((MPLexer) getScanner()).getLine();
   }
:};

terminal MAIN, INT, CHAR, FLOAT, CASE, WHEN;
terminal ID, CONST, PLUS, MINUS, ASSIGN;
terminal LPAREN, RPAREN, LBRACE, RBRACE, SEMICOLON, COMMA, COLON;

non terminal Program, Block, Declarations, VarDecl, Type, NameList;
non terminal StatementList, Statement, CaseStatement, WhenStatementList, WhenStatement;
non terminal Expression, Term, AddOperator;

start with Program;

Program ::= MAIN LPAREN RPAREN Block
          | MAIN LPAREN error RPAREN Block
            {: System.out.println("Greska u liniji " + parser.getLine() + ": Losi argumenti main funkcije."); parser.errNo++; :}
          | MAIN error Block
            {: System.out.println("Greska u liniji " + parser.getLine() + ": Nedostaju zagrade () kod main."); parser.errNo++; :}
          | error Block
            {: System.out.println("Greska u liniji " + parser.getLine() + ": Program mora poceti sa 'main'."); parser.errNo++; :}
          ;

Block ::= LBRACE Declarations StatementList RBRACE ;

Declarations ::= Declarations VarDecl 
               | Declarations error SEMICOLON
                 {: System.out.println("Greska u liniji " + parser.getLine() + ": Neispravna deklaracija promenljive."); parser.errNo++; :}
               | /* epsilon */ ;

VarDecl ::= Type NameList SEMICOLON 
          | Type NameList error
            {: System.out.println("Greska u liniji " + parser.getLine() + ": Nedostaje ';' nakon deklaracije."); parser.errNo++; :}
          ;

NameList ::= ID 
           | NameList COMMA ID 
           | NameList COMMA error
             {: System.out.println("Greska u liniji " + parser.getLine() + ": Nedostaje identifikator nakon zareza."); parser.errNo++; :}
           ;

Type ::= INT | CHAR | FLOAT ;

StatementList ::= StatementList Statement 
                | Statement ;

Statement ::= CaseStatement 
            | ID ASSIGN Expression SEMICOLON 
            | ID ASSIGN Expression error
              {: System.out.println("Greska u liniji " + parser.getLine() + ": Nedostaje ';' na kraju naredbe dodele."); parser.errNo++; :}
            | ID ASSIGN error SEMICOLON
              {: System.out.println("Greska u liniji " + parser.getLine() + ": Neispravan izraz u dodeli."); parser.errNo++; :}
            | Block ;

CaseStatement ::= CASE LPAREN Expression RPAREN LBRACE WhenStatementList RBRACE
                | CASE LPAREN Expression error LBRACE WhenStatementList RBRACE
                  {: System.out.println("Greska u liniji " + parser.getLine() + ": Nedostaje ')' u case naredbi."); parser.errNo++; :}
                | CASE error LBRACE WhenStatementList RBRACE
                  {: System.out.println("Greska u liniji " + parser.getLine() + ": Neispravan uslov u case naredbi."); parser.errNo++; :}
                ;

WhenStatementList ::= WhenStatementList WhenStatement 
                    | WhenStatement ;

WhenStatement ::= WHEN CONST COLON Statement 
                | WHEN CONST error Statement
                  {: System.out.println("Greska u liniji " + parser.getLine() + ": Nedostaje ':' nakon 'when' konstante."); parser.errNo++; :}
                | WHEN error COLON Statement
                  {: System.out.println("Greska u liniji " + parser.getLine() + ": Neispravna konstanta u 'when' uslovu."); parser.errNo++; :}
                ;

Expression ::= Expression AddOperator Term 
             | Term ;

AddOperator ::= PLUS | MINUS ;

Term ::= ID | CONST | LPAREN Expression RPAREN 
       | LPAREN Expression error
         {: System.out.println("Greska u liniji " + parser.getLine() + ": Nezatvorena zagrada u izrazu."); parser.errNo++; :}
       ;