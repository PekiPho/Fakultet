package lab1;

import java_cup.runtime.*;
import java.io.*;

parser code {:
   public int errNo = 0;
   
   public static void main( String[] args )
   {
      try
      {
         FileReader file = new FileReader( args[0] );
         MPLexer scanner = new MPLexer( file );
         MPParser parser = new MPParser( scanner );
         parser.parse();
         if ( parser.errNo == 0 )
            System.out.println( "Analiza zavrsena. U kodu nema gresaka." );
         else
            System.out.println( "Analiza zavrsena. Broj gresaka: " + parser.errNo );
      }
      catch( Exception e )
      {
         e.printStackTrace();
      }
   }
   
   public void syntax_error(Symbol cur_token)
   {
        System.out.println("Sintaksna greska u liniji " + (cur_token.left + 1) + ": " + cur_token.value);
        errNo++;
   }
   
   public int getLine() {
      return ((MPLexer) getScanner()).getLine();
   }
:};

terminal MAIN, INT, CHAR, FLOAT, CASE, WHEN;
terminal ID, CONST, PLUS, MINUS, ASSIGN;
terminal LPAREN, RPAREN, LBRACE, RBRACE, SEMICOLON, COMMA, COLON;

non terminal Program, Block, Declarations, VarDecl, Type, NameList;
non terminal StatementList, Statement, CaseStatement, WhenStatementList, WhenStatement;
non terminal Expression, Term, AddOperator;

start with Program;

// Gramatika sa error simbolima za oporavak
Program ::= MAIN LPAREN RPAREN Block ;

Block ::= LBRACE Declarations StatementList RBRACE ;

Declarations ::= Declarations VarDecl 
               | /* epsilon */ ;

VarDecl ::= Type NameList SEMICOLON
          | error SEMICOLON {: System.out.println("Greska u liniji " + parser.getLine() + ": Los nacin deklaracije."); parser.errNo++; :} ;

NameList ::= ID 
           | NameList COMMA ID ;

Type ::= INT | CHAR | FLOAT ;

StatementList ::= StatementList Statement 
                | Statement ;

Statement ::= CaseStatement 
            | ID ASSIGN Expression SEMICOLON 
            | ID ASSIGN error SEMICOLON {: System.out.println("Greska u liniji " + parser.getLine() + ": Neispravan izraz dodele."); parser.errNo++; :}
            | Block 
            | error SEMICOLON {: System.out.println("Greska u liniji " + parser.getLine() + ": Neispravna naredba."); parser.errNo++; :} ;

CaseStatement ::= CASE LPAREN Expression RPAREN LBRACE WhenStatementList RBRACE ;

WhenStatementList ::= WhenStatementList WhenStatement 
                    | WhenStatement ;

WhenStatement ::= WHEN CONST COLON Statement 
                | WHEN error COLON Statement {: System.out.println("Greska u liniji " + parser.getLine() + ": Los When uslov."); parser.errNo++; :} ;

Expression ::= Expression AddOperator Term 
             | Term ;

AddOperator ::= PLUS | MINUS ;

Term ::= ID | CONST | LPAREN Expression RPAREN ;